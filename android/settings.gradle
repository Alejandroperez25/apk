pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    def flutterSdkPath = {
        def envFlutterSdk = System.getenv('FLUTTER_SDK')
        if (envFlutterSdk != null) {
            return envFlutterSdk
        }

        def properties = new Properties()
        def localPropertiesFile = new File(rootDir, 'local.properties')
        if (localPropertiesFile.exists()) {
            localPropertiesFile.withReader('UTF-8') { reader -> properties.load(reader) }
        }

        def flutterLocalSdk = properties.getProperty('flutter.sdk')
        if (flutterLocalSdk != null) {
            return flutterLocalSdk
        }

        throw new GradleException('Flutter SDK not found. Set FLUTTER_SDK environment variable or flutter.sdk in local.properties.')
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.2.1" apply false
    id "org.jetbrains.kotlin.android" version "1.9.22" apply false
}

include ':app'
